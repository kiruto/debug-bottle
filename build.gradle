// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        snapshot = Boolean.valueOf(SNAPSHOT)
        buildCode = new Date().format('yyMMdd')
        buildName = Long.toHexString(Long.valueOf(new Date().format('yyMMddhhmmss'))).toUpperCase()
        versionUploaded = "$VERSION_NAME-support22"
        versionName = "$versionUploaded build $buildCode"
        // Version of Demo App in Google Play
        versionCode = Integer.valueOf(buildCode + VERSION_CODE)
        mavenVersionName = "$versionUploaded${if (snapshot) ".$buildName-SNAPSHOT" else ""}"

        println("*************** PRE BUILD INFO ***************")
        println("- maven version: $mavenVersionName")
        println("- bottle version: $versionUploaded")
        println("- google play version code: $versionCode")
        println("- build name: $versionName")
        println("- build code: $buildName")
        println("**********************************************")

        minSdkVersion = Integer.valueOf(MIN_SDK)
        compileSdkVersion = Integer.valueOf(COMPILE_SDK)
        targetSdkVersion = Integer.valueOf(TARGET_SDK)
        buildToolsVersion = BUILD_TOOLS
        javaVersion = JavaVersion.VERSION_1_7

        libraries = [
                gradle: '2.2.1',
                kotlin: '1.0.5',
                support: '22.2.1',
                support23: '23.4.0',
                junit: '4.12',
                leakcanary: '1.4',
                okhttp: '2.4.0',
                debugbottle: versionUploaded
        ]
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${libraries.gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${libraries.kotlin}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    // Used for upload
    version = VERSION_NAME
    group = GROUP

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}